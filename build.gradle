plugins {
    id 'org.ajoberstar.grgit' version '5.0.0'
}

allprojects {
    apply plugin: 'java'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenLocal()
        maven {
            url 'https://github.com/MegaMek/mavenrepo/raw/master'
        }
        mavenCentral()
        maven {
            url 'https://jitpack.io'
        }
    }
}

subprojects {
    group = 'org.megamek'
    version = '0.49.20-SNAPSHOT'
}

ext {
    mmGitRoot = 'https://github.com/MegaMek/megamek.git'
    mmlGitRoot = 'https://github.com/MegaMek/megameklab.git'
    mhqGitRoot = 'https://github.com/MegaMek/mekhq.git'
    // Work on MML or MHQ sometimes requires changes in MM as well. The maven publishing tasks use
    // these properties to append the branch name to the artifact id if the repo is not in the master
    // branch, making it available separately to the child project. 
    mhqBranch = grgit.branch.current().name
    mhqBranchTag = mhqBranch.equals('master') ? '' : '-' + mhqBranch
    
    // Allows setting a dependency on a different MM branch.
    mmBranch = 'master'
    mmBranchTag = mmBranch.equals('master') ? '' : '-' + mmBranch
    mmlBranch = 'master'
    mmlBranchTag = mmlBranch.equals('master') ? '' : '-' + mmlBranch
    
    mmDir = "${rootDir}/../megamek"
    mmlDir = "${rootDir}/../megameklab"
}

// A properties_local.gradle file can be used to override any of the above options. For instance,
// rootProject.ext.hqGitRoot = 'file:///path/to/local/repo' will cause the release target to clone a
// local copy of the repository rather than downloading it.

def localProperties = file('properties_local.gradle')
if (localProperties.exists()) {
    apply from: localProperties
}
