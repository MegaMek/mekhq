import edu.sc.seis.launch4j.tasks.Launch4jLibraryTask

import java.time.LocalDateTime

plugins {
    id 'application'
    id 'maven-publish'
    id 'edu.sc.seis.launch4j' version '3.0.5'
    id 'jacoco'
    id 'java'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['resources']
        }
    }
    test {
        java {
            srcDirs = ['unittests']
        }
        resources {
            srcDirs = ['testresources']
        }
    }
}

dependencies {
    implementation "org.megamek:megamek${mmBranchTag}:${version}"
    implementation ("org.megamek:megameklab${mmlBranchTag}:${version}") {
        // We may want to specify different branches for MM and MML, so we need to exclude the transitive MM dependency
        exclude group: 'org.megamek', module: "megamek${mmBranchTag}:${version}"

        // We don't need the python and javascript engine taking up space
        exclude group: 'org.python', module: 'jython'
        exclude group: 'org.mozilla', module: 'rhino'

        // Eclipse IDE Multiple Dependency errors.
        exclude group: 'xml-apis'
    }

    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    implementation 'javax.vecmath:vecmath:1.5.2'
    implementation 'joda-time:joda-time:2.12.7'
    implementation 'org.apache.commons:commons-csv:1.11.0'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'org.apache.commons:commons-text:1.12.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'
    implementation 'org.commonmark:commonmark:0.22.0'
    implementation 'org.jfree:jfreechart:1.5.4'
    implementation 'org.joda:joda-money:1.0.4'

    runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.5'
    // Required for mml printing scaled vector graphics (SVG) - Eclipse IDE Compatability.
    runtimeOnly 'xml-apis:xml-apis-ext:1.3.04'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.12.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
}

application {
    mainClass = 'mekhq.MekHQ'
}

ext {
    mmJvmOptions = [
        '-Xmx2048m',
        '--add-opens java.base/java.util=ALL-UNNAMED',
        '--add-opens java.base/java.util.concurrent=ALL-UNNAMED',
        '-Dsun.awt.disablegrab=true'
    ]
    mmlJvmOptions = [
        '-Xmx2048m',
        '--add-opens java.base/java.util=ALL-UNNAMED',
        '--add-opens java.base/java.util.concurrent=ALL-UNNAMED',
        '-Dsun.awt.disablegrab=true'
    ]
    mhqJvmOptions = [
        '-Xmx2048m',
        '--add-opens java.base/java.util=ALL-UNNAMED',
        '--add-opens java.base/java.util.concurrent=ALL-UNNAMED',
        '-Dsun.awt.disablegrab=true'
    ]
    campaigns = 'campaigns'
    data = 'data'
    docs = 'docs'
    lib = 'lib'
    log = 'logs'
    mmconf = 'mmconf'
    plugins = 'plugins'
    userdata = 'userdata'
    distributionDir = "${buildDir}/distributions"
    fileStagingDir = "${buildDir}/files"
    mmRepoDir = "${buildDir}/repo/megamek"
    mmlRepoDir = "${buildDir}/repo/megameklab"
    mhqRepoDir = "${buildDir}/repo/mekhq"
    scriptsDir = "${projectDir}/scripts"
    scriptTemplate = "${scriptsDir}/startScriptTemplate.txt"
}

run {
    jvmArgs = mhqJvmOptions
}

task mmJar (type: Jar) {
    description = 'Builds a MegaMek jar from the current source'
    dependsOn gradle.includedBuild('megamek').task(':megamek:compileTestJava')

    archiveFileName = "MegaMek.jar"
    from "${mmDir}/megamek/build/classes/java/main"
    from "${mmDir}/megamek/build/resources/main"
    manifest {
        attributes "Main-Class" : 'megamek.MegaMek'
        attributes "Class-Path" : project.sourceSets.main.runtimeClasspath.files
                .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek") }
                .collect { "${lib}/${it.name}" }.join(' ')
        attributes "Add-Opens" : 'java.base/java.util java.base/java.util.concurrent'
        attributes "Build-Date" : LocalDateTime.now()
    }
    ext.jarLocation = "${buildDir}/libs/${archiveFileName.get()}"
    inputs.dir "${mmDir}/megamek/build/classes/java/main"
    inputs.dir "${mmDir}/megamek/build/resources/main"
    outputs.file jarLocation
}

task mmlJar (type: Jar) {
    archiveFileName = "MegaMekLab.jar"
    dependsOn gradle.includedBuild('megameklab').task(':megameklab:compileTestJava')
    from "${mmlDir}/megameklab/build/classes/java/main"
    from "${mmlDir}/megameklab/build/resources/main"
    manifest {
        attributes "Main-Class" : 'megameklab.MegaMekLab'
        attributes "Class-Path" : "${mmJar.archiveFileName.get()} " + (project.sourceSets.main.runtimeClasspath.files
            .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek")}
            .collect { "${lib}/${it.name}" }.join(' '))
        attributes "Add-Opens" : 'java.base/java.util java.base/java.util.concurrent'
        attributes "Build-Date" : LocalDateTime.now()
    }
    ext.jarLocation = "${buildDir}/libs/${archiveFileName.get()}"
    inputs.dir "${mmlDir}/megameklab/build/classes/java/main"
    inputs.dir "${mmlDir}/megameklab/build/resources/main"
    outputs.file jarLocation
}

jar {
    archiveFileName = "MekHQ.jar"

    manifest {
        attributes "Main-Class" : application.mainClass
        attributes "Class-Path" : "${mmJar.archiveFileName.get()} ${mmlJar.archiveFileName.get()} " + (project.sourceSets.main.runtimeClasspath.files
            .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek") }
            .collect { "${lib}/${it.name}" }.join(' '))
        attributes "Add-Opens" : 'java.base/java.util java.base/java.util.concurrent'
        attributes "Build-Date" : LocalDateTime.now()
    }
}

task stageFiles(type: Copy) {
    description = 'Stages files that are to be copied into the distribution.'

    from projectDir
    include "${campaigns}/"
    include "${data}/fonts/"
    include "${data}/universe/"
    include "${data}/mapgen/"
    include "${data}/names/bloodnames/"
    include "${data}/images/awards/"
    include "${data}/images/force/"
    include "${data}/images/portraits/"
    include "${data}/images/fluff/Mech/*.png"
    include "${data}/images/misc/"
    include "${data}/images/stratcon/"
    include "${data}/images/universe/"
    include "${data}/images/storysplash/"
    include "${data}/scenariomodifiers/"
    include "${data}/scenariotemplates/"
    include "${data}/stratconbiomedefinitions/"
    include "${data}/stratconcontractdefinitions/"
    include "${data}/stratconfacilities/"
    include "${data}/storyarcs/"
    include "${data}/terrainconditionsodds/"
    include "${docs}/**"
    include "${mmconf}/**"
    include "${project.ext.plugins}/"
    include "${userdata}"
    include "${userdata}/data/universe/"
    include 'license.txt'
    include 'SubmitBug.html'

    into fileStagingDir

    inputs.dir "${campaigns}"
    inputs.dir "${data}"
    inputs.dir "${docs}"
    inputs.dir "${mmconf}"
    inputs.dir "${project.ext.plugins}"
    inputs.files 'license.txt', 'SubmitBug.html'
    outputs.dir fileStagingDir

    doLast {
        mkdir "${fileStagingDir}/${log}"
        mkdir "${fileStagingDir}/${userdata}/${mmconf}/campaignPresets/"
    }
}

task createStartScripts (type: CreateStartScripts) {
    description = 'Create shell script for generic distribution.'
    applicationName = 'mhq'
    mainClass = application.mainClass
    outputDir = startScripts.outputDir
    classpath = jar.outputs.files + files(project.sourceSets.main.runtimeClasspath.files)
            .filter { it.name.endsWith(".jar") }
    defaultJvmOpts = project.ext.mhqJvmOptions
    // The default template assumes the start script is in a subdirectory and always
    // sets APP_HOME to the parent directory of the one containing the script.
    // So we provide a modified template.
    unixStartScriptGenerator.template = resources.text.fromFile(scriptTemplate)
    doLast {
        // The start script assumes all the files on the classpath will be in the lib directory.
        unixScript.text = unixScript.text.replace('lib/MekHQ', 'MekHQ')
        windowsScript.text = windowsScript.text.replace('lib/MekHQ', 'MekHQ')
        // The Windows script just has a single line that needs to be changed to put the script in the root,
        // so we'll do a simple replace
        windowsScript.text = windowsScript.text.replace('set APP_HOME=%DIRNAME%..', '')
    }
}

distributions {
    // Creates tasks which package distribution with MM jar and startup script in root directory
    unix {
       distributionBaseName = 'mekhq-unix'
        contents {
            from ("${mmDir}/megamek/build/files")
            from ("${mmDir}/megamek/build/scripts") {
                include 'mm*'
                rename 'mm(.*)', 'mm-startup$1'
            }
            from ("${mmDir}/megamek/${docs}/history.txt") {
                rename 'history.txt', 'mm-history.txt'
                into 'docs'
            }
            from ("${mmDir}/megamek/${mmconf}/log4j2.xml") {
                exclude 'log4j2.xml'
            }
            from ("${mmlDir}/megameklab/build/files/${data}/images") {
                into "${data}/images"
            }
            from ("${mmlDir}/megameklab/build/scripts") {
                include 'mml*'
                rename 'mml(.*)', 'mml-startup$1'
            }
            from ("${mmlDir}/megameklab/${docs}/history.txt") {
                rename 'history.txt', 'mml-history.txt'
                into "${docs}"
            }
            from ("${mmlDir}/megameklab/${mmconf}/log4j2.xml") {
                exclude 'log4j2.xml'
            }
            from ("docs/history.txt") {
                rename 'history.txt', 'mhq-history.txt'
                into 'docs'
            }
            from (fileStagingDir) {
                exclude 'history.txt'
            }
            from (createStartScripts) {
                include 'mhq*'
                rename 'mhq(.*)', 'mhq-startup$1'
            }
            from (project.sourceSets.main.runtimeClasspath.files
                    .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek") }) {
                into "${lib}"
            }
            from (mmJar)
            from (mmlJar)
            from (jar)
            duplicatesStrategy = 'exclude'
        }
    }
    // Creates tasks which package distribution with MM jar wrapped in Windows executable
    windows {
        distributionBaseName = 'mekhq-windows'
        contents {
            from ("${mmDir}/megamek/build/files")
            from ("${mmDir}/megamek/docs/history.txt") {
                rename 'history.txt', 'mm-history.txt'
                into 'docs'
            }
            from ("${mmDir}/megamek/${mmconf}/log4j2.xml") {
                exclude 'log4j2.xml'
            }
            from ("${mmlDir}/megameklab/build/files/${data}/images") {
                into "${data}/images"
            }
            from ("${mmlDir}/megameklab/${docs}/history.txt") {
                rename 'history.txt', 'mml-history.txt'
                into "${docs}"
            }
            from ("${mmlDir}/megameklab/${mmconf}/log4j2.xml") {
                exclude 'log4j2.xml'
            }
            from ("docs/history.txt") {
                rename 'history.txt', 'mhq-history.txt'
                into 'docs'
            }
            from (fileStagingDir) {
                exclude 'history.txt'
            }
            from ("${buildDir}/launch4j") {
                include '*.exe'
                include '*.ini'
            }
            from (project.sourceSets.main.runtimeClasspath.files
                    .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek") }) {
                into "${lib}"
            }
            from (mmJar) {
                into "${lib}"
            }
            from (mmlJar) {
                into "${lib}"
            }
            from(jar)
            duplicatesStrategy = 'exclude'
        }
    }
}

createExe {
    description = 'Create Windows executable that wraps MekHQ jar'
    mainClassName = application.mainClass
    outfile = 'MekHQ.exe'
    def iniFile = 'MekHQ.l4j.ini'
    icon = "${projectDir}/${data}/images/misc/mekhq.ico"
    jarTask = project.tasks.jar
    internalName = 'MekHQ'
    downloadUrl = 'https://github.com/MegaMek/megamek/wiki/Updating-to-Adoptium'
    supportUrl = 'https://megamek.org'
    copyright = '2024 MegaMek Development Team'
    companyName = "MegaMek Development Team"
    outputs.file "${buildDir}/launch4j/${outfile}"
    outputs.file "${buildDir}/launch4j/${iniFile}"
    jvmOptions = project.ext.mhqJvmOptions
    jreMinVersion = '17'
    messagesJreNotFoundError = 'Go here for instructions on installing the correct version of Java: https://github.com/MegaMek/megamek/wiki/Updating-to-Adoptium'
    classpath = ["lib/${mmJar.archiveFileName.get()}", "lib/${mmlJar.archiveFileName.get()}"] + project.sourceSets.main.runtimeClasspath.files
            .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek")}
            .collect { "${lib}/${it.name}" }
    doLast {
        new File("${buildDir}/launch4j/${iniFile}").text = """# Launch4j runtime config
# you can add arguments here that will be processed by the JVM at runtime
${project.ext.mhqJvmOptions.join('\n')}
"""
    }
}

task createMMExe (type: Launch4jLibraryTask, dependsOn: mmJar) {
    description = 'Create Windows executable stub for MM jar'
    mainClassName = 'megamek.MegaMek'
    outfile = 'MegaMek.exe'
    def iniFile = 'MegaMek.l4j.ini'
    icon = "${mmDir}/megamek/${data}/images/misc/megamek.ico"
    jarTask = project.tasks.mmJar
    internalName = 'MegaMek'
    downloadUrl = 'https://github.com/MegaMek/megamek/wiki/Updating-to-Adoptium'
    supportUrl = 'https://megamek.org'
    copyright = '2024 MegaMek Development Team'
    companyName = "MegaMek Development Team"
    outputs.file "${buildDir}/launch4j/${outfile}"
    outputs.file "${buildDir}/launch4j/${iniFile}"
    classpath = project.sourceSets.main.runtimeClasspath.files
            .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek")}
            .collect { "${lib}/${it.name}" }
    jvmOptions = project.ext.mmJvmOptions
    jreMinVersion = '17'
    messagesJreNotFoundError = 'Go here for instructions on installing the correct version of Java: https://github.com/MegaMek/megamek/wiki/Updating-to-Adoptium'
    dontWrapJar = true
    doLast {
        new File("${buildDir}/launch4j/${iniFile}").text = """# Launch4j runtime config
# you can add arguments here that will be processed by the JVM at runtime
${project.ext.mmJvmOptions.join('\n')}
"""
    }
}

task createMMLExe (type: Launch4jLibraryTask, dependsOn: mmlJar) {
    description = 'Create Windows executable stub for MML jar'
    mainClassName = 'megameklab.MegaMekLab'
    outfile = 'MegaMekLab.exe'
    def iniFile = 'MegaMekLab.l4j.ini'
    jarTask = project.tasks.mmlJar
    classpath = ["lib/${mmJar.archiveFileName.get()}"] + project.sourceSets.main.runtimeClasspath.files
            .findAll { it.name.endsWith(".jar") && !it.name.toLowerCase().startsWith("megamek")}
            .collect { "${lib}/${it.name}" }
    icon = "${mmlDir}/megameklab/${data}/images/misc/megameklab.ico"
    internalName = 'MegaMekLab'
    downloadUrl = 'https://github.com/MegaMek/megamek/wiki/Updating-to-Adoptium'
    supportUrl = 'https://megamek.org'
    copyright = '2024 MegaMek Development Team'
    companyName = "MegaMek Development Team"
    jvmOptions = project.ext.mmlJvmOptions
    jreMinVersion = '17'
    messagesJreNotFoundError = 'Go here for instructions on installing the correct version of Java: https://github.com/MegaMek/megamek/wiki/Updating-to-Adoptium'
    dontWrapJar = true
    outputs.file "${buildDir}/launch4j/${outfile}"
    outputs.file "${buildDir}/launch4j/${iniFile}"
    doLast {
        new File("${buildDir}/launch4j/${iniFile}").text = """# Launch4j runtime config
# you can add arguments here that will be processed by the JVM at runtime
${project.ext.mmlJvmOptions.join('\n')}
"""
    }
}

task stageMM (type: GradleBuild) {
    description = 'Prepares files from the MegaMek and MegaMekLab projects'
    group = 'distribution'

    dependsOn gradle.includedBuild('megamek').task(':megamek:stageFiles')
    dependsOn gradle.includedBuild('megamek').task(':megamek:createStartScripts')
    dependsOn gradle.includedBuild('megameklab').task(':megameklab:stageFiles')
    dependsOn gradle.includedBuild('megameklab').task(':megameklab:createStartScripts')

    dir = "${mmDir}"
}

windowsDistZip {
    description = 'Creates Windows distribution packaged as a zip archive'
    dependsOn stageFiles
    dependsOn stageMM
    dependsOn createExe
    dependsOn createMMExe
    dependsOn createMMLExe
    dependsOn startScripts
}

windowsDistTar {
    description = 'Creates Windows distribution packaged as a zip archive'
    dependsOn stageFiles
    dependsOn stageMM
    dependsOn createExe
    dependsOn createMMExe
    dependsOn createMMLExe
    dependsOn startScripts
    archiveExtension = 'tar.gz'
    compression = Compression.GZIP
}

unixDistTar {
    description = 'Creates *nix distribution packaged as a tar ball'
    dependsOn stageFiles
    dependsOn stageMM
    dependsOn startScripts
    archiveExtension = 'tar.gz'
    compression = Compression.GZIP
}

unixDistZip {
    description = 'Creates *nix distribution packaged as a tar ball'
    dependsOn stageFiles
    dependsOn stageMM
    dependsOn createStartScripts
    dependsOn startScripts
}

distTar {
    description = 'Creates *nix distribution packaged as a tar ball'
    dependsOn stageFiles
    dependsOn stageMM
    dependsOn createStartScripts
    dependsOn startScripts
    archiveExtension = 'tar.gz'
    compression = Compression.GZIP
}

distZip {
    description = 'Creates *nix distribution packaged as a tar ball'
    dependsOn stageFiles
    dependsOn stageMM
    dependsOn createStartScripts
    dependsOn startScripts
}

// The repository can only be cloned into an empty directory so we need to delete anything left over
// from a previous build
task deleteMMRepoDir (type: Delete) {
    delete = "${mmRepoDir}"
}

task deleteMMLRepoDir (type: Delete) {
    delete = "${mmlRepoDir}"
}

task deleteMHQRepoDir (type: Delete) {
    delete = "${mhqRepoDir}"
}

task cloneMMRepo (type: Exec, dependsOn: deleteMMRepoDir) {
    description = 'Clones the MegaMek repository to ensure we are working with a clean directory tree'
    group = 'distribution'
    commandLine "git"
    args "clone", "--branch", mmBranch, "--single-branch", mmGitRoot, "${mmRepoDir}"
}

task cloneMMLRepo (type: Exec, dependsOn: deleteMMLRepoDir) {
    description = 'Clones the MegaMekLab repository to ensure we are working with a clean directory tree'
    group = 'distribution'
    commandLine "git"
    args "clone", "--branch", mmlBranch, "--single-branch", mmlGitRoot, "${mmlRepoDir}"
}

task cloneMHQRepo (type: Exec, dependsOn: deleteMHQRepoDir) {
    description = 'Clones the MekHQ repository to ensure we are working with a clean directory tree'
    group = 'distribution'
    commandLine "git"
    args "clone", "--branch", mhqBranch, "--single-branch", mhqGitRoot, "${mhqRepoDir}"
}

task sourceZip (type: Zip) {
    description 'Creates a zip archive of the source'
    group = 'distribution'
    from (rootDir) {
        exclude "**/build/**"
        exclude "**/.*"
    }
    archiveFileName = "mekhq-${archiveVersion}-source.zip"
    destinationDirectory = file(distributionDir)
}

task sourceTar (type: Tar) {
    description 'Creates a compressed tar archive of the source'
    from (rootDir) {
        exclude "**/build/**"
        exclude "**/.*"
    }
    archiveFileName = "mekhq-${archiveVersion}-source.tar.gz"
    compression = Compression.GZIP
    destinationDirectory = file(distributionDir)
}

// The distribution plugin adds the distro packages to the assemble task, which causes the build task
// to run all the packaging tasks.
task assemble(overwrite: true) {
    dependsOn jar
}

// Replace the assembleDist task created by the distributions plugin to create the packages we want to
task assembleDist(overwrite: true) {
    description = 'Build unix, Windows, and source packages'
    group = 'distribution'
    dependsOn stageMM
    dependsOn test
    dependsOn unixDistTar
    dependsOn windowsDistZip
}

task buildFromRepo (type: GradleBuild) {
    description = 'Assembles the distribution packages in the clean repository copy'
    group = 'distribution'
    dependsOn cloneMMRepo
    dependsOn cloneMMLRepo
    dependsOn cloneMHQRepo

    buildName = "mhq-build-repo"
    dir = "${mhqRepoDir}"
    tasks = [ 'assembleDist' ]
}

task release (type: Copy) {
    description = 'Builds the release packages from the repository and copies them into the project build directory'
    group = 'distribution'
    dependsOn buildFromRepo

    from "${mhqRepoDir}/MekHQ/build/distributions"
    into "${distributionDir}"
}

task buildMM (type: GradleBuild, dependsOn: cloneMMRepo) {
    description = 'Prepares files from the MegaMek project'
    group = 'distribution'

    buildName = "mhq-build-mm"
    dir = "${mmRepoDir}"
    tasks = [ 'assembleDist' ]
}

task buildMML (type: GradleBuild, dependsOn: cloneMMLRepo) {
    description = 'Prepares files from the MegaMekLab project'
    group = 'distribution'

    buildName = "mhq-build-mml"
    dir = "${mmlRepoDir}"
    tasks = [ 'assembleDist' ]
}

task buildAllFromRepo (type: GradleBuild) {
    description = 'Assembles the distribution packages in the clean repository copy'
    group = 'distribution'
    dependsOn cloneMMRepo
    dependsOn cloneMMLRepo
    dependsOn cloneMHQRepo
    dependsOn buildMM
    dependsOn buildMML

    buildName = "mhq-build-all-repo"
    dir = "${mhqRepoDir}"
    tasks = [ 'unixDistTar', 'windowsDistZip' ]
}

task releaseAll (type: Copy) {
    description = 'Builds the release packages for all the projects and copies them into the project build directory'
    group = 'distribution'
    dependsOn buildAllFromRepo

    from "${mmRepoDir}/megamek/build/distributions"
    from "${mmlRepoDir}/megameklab/build/distributions"
    from "${mhqRepoDir}/MekHQ/build/distributions"
    into "${distributionDir}"
}

task sourcesJar(type: Jar) {
    description = 'Creates a source archive for publication to a maven repository'
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    description = 'Creates a documentation archive for publication to a maven repository'
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        publishMMLibrary(MavenPublication) {
            artifactId = "mekhq${mmBranchTag}"
            from components.java
            artifact sourcesJar
//            artifact javadocJar

            pom {
                name = "MekHQ"
                description = "MekHQ"
                url = "https://megamek.org"
                licenses {
                    license {
                        name = "GNU General Public License, version 3"
                        url = "https://www.gnu.org/licenses/gpl-3.0.en.html"
                    }
                }
            }
        }
    }
}

test {
    useJUnitPlatform()
    // report is always generated after tests run
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    // tests are required to run before generating the report
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}
